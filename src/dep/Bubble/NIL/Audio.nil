class TAudioInstance
	readonly string buftag
	readonly string from
	bool AutoFree = true
	
	void CONSTRUCTOR(string audio,bool loop)
		self.buftag = Bubble_Audio:IPlay(audio,loop)
		print("Instance created for "..audio,"Loop: "..tostring(loop),"Instance tag: "..self.buftag)
		self.from = audio
	end
	
	void DESTRUCTOR()
		if self.AutoFree
			Bubble_Audio:FreeInstance(self.buftag)
			print("Instance destroyed for "..self.buftag)
		else
			print("Instance for "..self.buftag.." already destroyed, or otherwise blocked for destruction")
		end
	end
	
	void Stop()
		Bubble_Audio:StopInstance(self.buftag)
	end
	
	void Pause()
		Bubble_Audio:PauseInstance(self.buftag)
	end
	
	void Resume()
		Bubble_Audio:ResumeInstance(self.buftag)
	end
	
	void Free()
		print("Forcing to destroy instance: "..self.buftag)
		Bubble_Audio:FreeInstance(self.buftag)
		self.AutoFree = false
	end
	
end

class TAudio
	private bool dbg=true;

	private bool autodel
	private string buftag
	
	void CONSTRUCTOR(string file,buftag)
		if file!="*GET*"
			self.buftag = Bubble_Audio:Load(file,buftag or "")			
			self.autodel = buftag==nil;
			print("NIL: "..file.." stored in "..self.buftag)
		else
			assert(type(buftag)=="string","Want audio tag")
			self.buftag=buftag
		end
	end
	
	void DESTRUCTOR()
		if self.autodel 
			Bubble_Audio:Free(self.buftag) 
			if self.dbg then print("Audio buffer "..self.buftag.." released") end
		elseif self.dbg 
			print("Audio NIL object release, however I had no permission to remove "..self.buftag.." from the memory")
		end
	end
		
	void Free()
		Bubble_Audio:Free(self.buftag) self.autodel=false
		if self.dbg then print("Audio buffer "..self.buftag.." forcefully released") end
	end	
	
	void Play()
		Bubble_Audio:Play(self.buftag)
	end
	
	var IPlay()
		return new TAudioInstance(self.buftag,false)
	end
	
	var Loop()
		return new TAudioInstance(self.buftag,true)
	end
	

end


global string LoadedAdioBuffers()
	return Bubble_Audio:LoadedBuffers()
end

global var LoadAudio(string file,buftag)
	return new TAudio(file,buftag)
end
global var LoadSound
LoadSound=LoadAudio

global var GetAudio(string buftag)
	var ret
	ret = new TAudio("*GET*",buftag)
	return ret
end

global void FreeAudio(string buffer)
	Bubble_Audio:Free(buffer)
end

global var LoadNewAudio(string file, string buftag)
	Bubble_Audio:LoadIfNew(file,buftag)
	return GetAudio(buftag)
end
